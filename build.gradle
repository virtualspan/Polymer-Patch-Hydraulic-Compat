plugins {
    id 'fabric-loom' version "${loom_version}"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    mixin.defaultRefmapName = "hydraulicpolybridge.refmap.json"
}

repositories {
    // Polymer
    maven { url 'https://maven.nucleoid.xyz' }

    // Floodgate
    maven { url = uri("https://repo.opencollab.dev/main/") }

    // PolyMc
    maven { url "https://maven.theepicblock.nl"; content { includeGroup("nl.theepicblock") } }
    maven { url = uri("https://jitpack.io") }
}

dependencies {
    // Minecraft + mappings + loader
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Floodgate API
    modImplementation "org.geysermc.floodgate:api:${project.floodgate_version}"

    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Polymer
    modCompileOnly "eu.pb4:polymer-core:${project.polymer_version}"
    modCompileOnly "eu.pb4:polymer-blocks:${project.polymer_version}"

    // PolyMc fork https://github.com/Patbox/PolyMc/tree/dev/1.21.6
    modCompileOnly ('com.github.Patbox:PolyMc:dev~1.21.6-SNAPSHOT') {
        exclude(group: "org.quiltmc", module: "quilt-loader")
        exclude(group: "net.fabricmc.fabric-api")
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}